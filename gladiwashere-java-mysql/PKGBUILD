developer="http://indiecomputing.com/"
url="http://ubos.net/"
maintainer=${developer}
pkgname=$(basename $(pwd))
pkgver=0.13
pkgrel=1
pkgdesc="Best Guestbook Evar, in Java this time, using MySQL"
_groupId='net.ubos.ubos-toyapps'
arch=('any')
makedepends=('maven' 'jdk8-openjdk' )
depends=('java-runtime' 'diet4j' 'mysql-connector-java')
license=('AGPL3')
options=('!strip')
_java_home=/usr/lib/jvm/java-8-openjdk

prepare() {
    cd ${startdir}/maven
    sed -e "s/PKGBUILD_VERSION/${pkgver}/" pom.xml.tmpl > pom.xml
    JAVA_HOME=${_java_home} mvn clean install ${MVN_OPTS}
}

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/${pkgname}
    install -m644 ${startdir}/appicons/{72x72,144x144}.png $pkgdir/srv/http/_appicons/${pkgname}/
    install -m644 ${startdir}/appicons/license.txt $pkgdir/srv/http/_appicons/${pkgname}/

# Code
    install -m644 -D ${startdir}/maven/target/${pkgname}-${pkgver}.war \
                     ${pkgdir}/usr/lib/java/${_groupId//.//}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.war

# Templates, with package/module names and versions replaced
    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m755 ${startdir}/tmpl/context.xml.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/
    install -m755 ${startdir}/tmpl/htaccess.tmpl    ${pkgdir}/usr/share/${pkgname}/tmpl/

    groupId=${_groupId//./\\/}

    sed -i -e "s/\${pkgname}/${pkgname}/g" ${pkgdir}/usr/share/${pkgname}/tmpl/context.xml.tmpl
    sed -i -e "s/\${pkgver}/${pkgver}/g"   ${pkgdir}/usr/share/${pkgname}/tmpl/context.xml.tmpl
    sed -i -e "s/\${groupId}/${groupId}/g" ${pkgdir}/usr/share/${pkgname}/tmpl/context.xml.tmpl

    mkdir -p ${pkgdir}/usr/share/${pkgname}/sql
    install -m755 ${startdir}/sql/create.sql ${pkgdir}/usr/share/${pkgname}/sql/
}
